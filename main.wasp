app ToDoApp {
  wasp: { version: "^0.12.3" },
  title: "ToDo App",

  auth: {
    userEntity: User,
    methods: {
      gitHub: {},
      google: {},
      usernameAndPassword: {},
    },
    onAuthFailedRedirectTo: "/login",
  },

  client: {
    rootComponent: import Layout from "@src/client/components/MainLayout",
  },
}

// -<>-- // --<>- //

entity Task {=psl
  id                        Int           @id @default(autoincrement())
  description               String
  isDone                    Boolean       @default(false)
psl=}

entity User {=psl
  id                        Int           @id @default(autoincrement())
psl=}

// -<>-- // --<>- //

crud Tasks {
  entity: Task,
  operations: {
    getAll: {},
    create: {},
    update: {},
    delete: {},
  },
}

// -<>-- // --<>- //

route RootRoute { path: "/", to: MainPage }
page MainPage {
  authRequired: true,
  component: import Main from "@src/client/MainPage"
}

route LoginRoute { path: "/login", to: LoginPage }
page LoginPage {
  component: import { LoginPage } from "@src/client/auth/LoginPage"
}

route SignupRoute { path: "/signup", to: SignupPage }
page SignupPage {
  component: import { SignupPage } from "@src/client/auth/SignupPage"
}
